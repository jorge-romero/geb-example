plugins {
    id 'groovy'
    id 'java'
    id "com.github.erdi.webdriver-binaries" version "2.2"
    id "com.github.erdi.idea-base" version "2.2"
    id 'com.adarshr.test-logger' version "2.0.0"
}

ext {
    drivers = ["chrome", "chromeHeadless"]
    spockVersion = "1.3-groovy-2.5"
    gebVersion = '3.4'
    seleniumVersion = '3.141.59'
    chromeDriverVersion = '86.0.4240.22'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    testCompile "org.gebish:geb-spock:${gebVersion}"
    testCompile("org.spockframework:spock-core:1.3-groovy-2.5") {
        exclude group: "org.codehaus.groovy"
    }

    // If using JUnit, need to depend on geb-junit (3 or 4)
    testCompile "org.gebish:geb-junit4:$gebVersion"

    testCompile "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"

}

webdriverBinaries {
    chromedriver chromeDriverVersion
    fallbackTo32Bit = true
    driverUrlsConfiguration = resources.text.fromFile('repository-3.0.json')

    drivers.each { driver ->
        task "${driver}Test"(type: Test) {
            group JavaBasePlugin.VERIFICATION_GROUP
            outputs.upToDateWhen { false }  // Always run tests
            systemProperty "geb.build.reportsDir", reporting.file("geb/$name")
            systemProperty "geb.env", driver
        }
    }
}

test {
    dependsOn drivers.collect { tasks["${it}Test"] }
    enabled = false
    systemProperties System.properties
}

tasks.withType(Test) {
    maxHeapSize = "1g"
    jvmArgs '-XX:MaxMetaspaceSize=128m'
    testLogging {
        exceptionFormat = 'full'
    }
}

tasks.withType(GroovyCompile) {
    groovyOptions.forkOptions.memoryMaximumSize = '256m'
}

testlogger {
    showStandardStreams true
}